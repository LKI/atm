group 'com.liriansu.atm'

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.7
def releaseDir = new File(buildDir, 'atm')

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'atm'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'atm'
        }
    }
}

dependencies {
    // https://mvnrepository.com/artifact/org.seleniumhq.selenium/selenium-java
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.53.1'

    // Scannotation, we use this to register Commands
    compile group: 'org.scannotation', name: 'scannotation', version: '1.0.3'

    compile group: 'jline', name: 'jline', version: '2.13'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    // Keep jar files clean
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

    // Set main class and class path
    manifest {
        attributes 'Main-Class': 'com.liriansu.atm.Main',
                'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
    }
}

task release(dependsOn: [build]) << {
    copy {
        from(configurations.runtime, jar)
        into new File(releaseDir, "lib")
    }
    copy {
        from new File("atm")
        into releaseDir
    }
}
